# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("config.gni")

# compile options for gcov
config("hiprofiler_test_config") {
  cflags = []
  if (enable_debuginfo) {
    cflags += [ "-g" ]
  }

  if (enable_coverage && current_toolchain == host_toolchain) {
    cflags += [
      # clang coverage options:
      "--coverage",
      "-mllvm",
      "-limited-coverage-experimental=true",
      "-fno-use-cxa-atexit",
    ]
    ldflags = [ "--coverage" ]
  }
}

config("hiprofiler_base_config") {
  include_dirs = [
    "include",
    "//utils/native/base/include"
  ]
}

ohos_source_set("hiprofiler_base") {
  sources = [
    "src/schedule_task_manager.cpp",
  ]

  public_configs = [
    ":hiprofiler_test_config",
    ":hiprofiler_base_config",
  ]
  if (current_toolchain != host_toolchain) {
    defines = [ "HAVE_HILOG" ]
    if (build_l2) {
      external_deps = [ "shared_library:libhilog" ]
    } else {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    }
  }
}
